task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

buildscript {
    project.ext {
        springBootPluginVersion = "2.1.1.RELEASE"
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$project.springBootPluginVersion")
    }
}

subprojects {
    group 'com.spring.test'
    version = '1.0'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:$project.springBootPluginVersion")
        }
        dependencies {
            dependencySet(group: "io.springfox", version: "2.9.2") {
                entry("springfox-swagger2")
                entry("springfox-swagger-ui")
            }
        }
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        compileOnly "org.springframework.boot:spring-boot-configuration-processor"
        compile("org.projectlombok:lombok:1.16.20")

        compile("io.springfox:springfox-swagger2")
        compile("io.springfox:springfox-swagger-ui")

        testCompile('org.springframework.boot:spring-boot-starter-test')
    }

}


